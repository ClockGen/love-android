plugins {
    id 'com.android.application'
}

android {
    compileSdk 31
    ndkVersion "23.1.7779620"

    defaultConfig {
        applicationId "org.love2d.android"
        versionCode 31
        versionName "12.0"
        minSdk 21
        targetSdk 31
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                targets "love"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'mode', 'recording'
    productFlavors {
        normal {
            dimension 'mode'
        }
        embed {
            dimension 'mode'
        }
        record {
            dimension 'recording'
        }
        noRecord {
            dimension 'recording'
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/cpp/megasource/libs/SDL2/android-project/app/src/main/java'
                srcDir 'src/main/java'
                srcDir 'src/main/cpp/love/src/libraries/luahttps/src/android/java'
            }
        }
        normal {
            java {
                srcDir 'src/normal/java'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            // '+' notation apparently has been supported long time ago
            // https://issuetracker.google.com/issues/110693527#comment22
            // We require CMake 3.21 because r23 has important fixes
            // that's only fixed if CMake 3.21 is used.
            // https://github.com/android/ndk/issues/463
            version '3.21.0+'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
}
